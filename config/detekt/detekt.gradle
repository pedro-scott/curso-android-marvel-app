apply plugin: 'io.gitlab.arturbosch.detekt'

project.afterEvaluate {
    tasks.named("preBuild") {
        dependsOn("detekt")
    }
}

detekt {
    toolVersion = "$detektVersion"
    def inputDirFiles = []
    rootProject.subprojects.each {module ->
        inputDirFiles << "$module.projectDir/src/main/java"
    }
    source = files(inputDirFiles)
    config = files("$rootDir/config/detekt/detekt.yml")
    autoCorrect = true
}

tasks.named("detekt").configure {
    reports {
        xml.required.set(true)
        xml.outputLocation.set(file("$buildDir/reports/detekt/detekt.xml"))

        html.required.set(true)
        html.outputLocation.set(file("$buildDir/reports/detekt/detekt.html"))

        txt.required.set(true)
        txt.outputLocation.set(file("$buildDir/reports/detekt/detekt.txt"))

        sarif.required.set(false)
    }
}
